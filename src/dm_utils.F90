!   Copyright 2016 University of Auckland.

!   This file is part of Waiwera.

!   Waiwera is free software: you can redistribute it and/or modify
!   it under the terms of the GNU Lesser General Public License as published by
!   the Free Software Foundation, either version 3 of the License, or
!   (at your option) any later version.

!   Waiwera is distributed in the hope that it will be useful,
!   but WITHOUT ANY WARRANTY; without even the implied warranty of
!   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!   GNU Lesser General Public License for more details.

!   You should have received a copy of the GNU Lesser General Public License
!   along with Waiwera.  If not, see <http://www.gnu.org/licenses/>.

module dm_utils_module
  !! Module for utilities related to PETSc DMs (which handle data management on parallel meshes), and Vecs.

#include <petsc/finclude/petsc.h>

  use petsc

  implicit none
  private

  type, public :: dm_stratum_type
     !! Type for point stratum (cells, faces, edges or vertices) in a
     !! DM. This includes functionality for MINC DM point calculations.
     private
     PetscInt, public :: start, end, end_interior
     PetscInt, public, allocatable :: minc_shift(:)
     PetscInt, public :: num_minc_points
   contains
     private
     procedure, public :: size => dm_stratum_size
     procedure, public :: contains_point => dm_stratum_contains_point
     procedure, public :: destroy => dm_stratum_destroy
     procedure :: minc_point_single => dm_stratum_minc_point_single
     procedure :: minc_point_array => dm_stratum_minc_point_array
     generic, public :: minc_point => minc_point_single, minc_point_array
  end type dm_stratum_type

  public :: dm_get_strata, dm_point_stratum_height

  public :: set_dm_data_layout, set_dm_default_data_layout
  public :: dm_setup_global_section
  public :: section_offset, global_section_offset

  interface natural_to_local_cell_index
     module procedure natural_to_local_cell_index_array
     module procedure natural_to_local_cell_index_single
  end interface natural_to_local_cell_index

  interface local_to_natural_cell_index
     module procedure local_to_natural_cell_index_single
     module procedure local_to_natural_cell_index_array
  end interface local_to_natural_cell_index

  public :: global_vec_section, local_vec_section
  public :: global_to_local_vec_section, restore_dm_local_vec
  public :: global_vec_range_start, vec_reorder
  public :: dm_cell_normal_face
  public :: write_vec_vtk
  public :: vec_max_pointwise_abs_scale
  public :: dm_set_fv_adjacency, dm_setup_fv_discretization
  public :: dm_get_num_partition_ghost_cells, dm_get_bdy_cell_shift
  public :: dm_get_end_interior_cell
  public :: dm_get_natural_to_global_ao, dm_get_cell_index
  public :: natural_to_local_cell_index, local_to_natural_cell_index
  public :: create_path_dm
  public :: get_field_subvector, section_get_field_names
  public :: dm_global_cell_field_dof

contains

!------------------------------------------------------------------------
! DM stratum type:
!------------------------------------------------------------------------

  PetscInt function dm_stratum_size(self) result(n)
    !! Returns size of DM stratum.

    class(dm_stratum_type), intent(in) :: self

    n = self%end - self%start

  end function dm_stratum_size

!------------------------------------------------------------------------

  PetscBool function dm_stratum_contains_point(self, p) result(has)
    !! Returns whether specified DM point p is inside the stratum.

    class(dm_stratum_type), intent(in) :: self
    PetscInt, intent(in) :: p

    has = ((self%start <= p) .and. (p < self%end))

  end function dm_stratum_contains_point

!------------------------------------------------------------------------

  subroutine dm_stratum_destroy(self)
    !! Destroys DM stratum.

    class(dm_stratum_type), intent(in out) :: self

    if (allocated(self%minc_shift)) then
       deallocate(self%minc_shift)
    end if

  end subroutine dm_stratum_destroy

!------------------------------------------------------------------------

  PetscInt function dm_stratum_minc_point_single(self, p, m) &
       result(minc_p)
    !! Returns point for MINC level m in MINC DM corresponding to
    !! point p in original DM. Boundary ghost cells are shifted up by
    !! the number of MINC points in the stratum. For MINC points (m >
    !! 0), p should be the index of the fracture cell in the list of
    !! fracture cells for the given MINC level.

    class(dm_stratum_type), intent(in) :: self
    PetscInt, intent(in) :: p, m

    minc_p = p + self%minc_shift(m)
    if ((m == 0) .and. (p >= self%end_interior)) then
       minc_p = minc_p + self%num_minc_points
    end if

  end function dm_stratum_minc_point_single

!........................................................................

  function dm_stratum_minc_point_array(self, p_array, m) &
       result(minc_p_array)
    !! Returns array of MINC points for array of original DM points.

    class(dm_stratum_type), intent(in) :: self
    PetscInt, intent(in) :: p_array(:), m
    PetscInt, allocatable :: minc_p_array(:)
    ! Locals:
    PetscInt :: p, i

    allocate(minc_p_array(size(p_array)))
    do i = 1, size(p_array)
       p = p_array(i)
       minc_p_array(i) = self%minc_point_single(p, m)
    end do

  end function dm_stratum_minc_point_array

!------------------------------------------------------------------------

  subroutine dm_get_strata(dm, depth, strata)
    !! Gets mesh depth and array of strata for the given DM.

    DM, intent(in) :: dm
    PetscInt, intent(out) :: depth
    type(dm_stratum_type), allocatable, intent(out) :: strata(:)
    ! Locals:
    PetscErrorCode :: ierr, h

    call DMPlexGetDepth(dm, depth, ierr); CHKERRQ(ierr)
    allocate(strata(0: depth))

    do h = 0, depth
       call DMPlexGetHeightStratum(dm, h, strata(h)%start, &
            strata(h)%end, ierr); CHKERRQ(ierr)
    end do

    call DMPlexGetHybridBounds(dm, strata(0)%end_interior, &
         strata(1)%end_interior, strata(depth - 1)%end_interior, &
         strata(depth)%end_interior, ierr); CHKERRQ(ierr)
    do h = 0, depth
       if (strata(h)%end_interior < 0) then
          strata(h)%end_interior = strata(h)%end
       end if
    end do

  end subroutine dm_get_strata

!------------------------------------------------------------------------

  PetscInt function dm_point_stratum_height(strata, p) result(height)
    !! Returns height of stratum containing point p, given array of
    !! strata.

    class(dm_stratum_type), intent(in) :: strata(0:)
    PetscInt, intent(in) :: p
    ! Locals:
    PetscInt :: h

    height = -1
    associate(depth => size(strata) - 1)
      do h = 0, depth
         if (strata(h)%contains_point(p)) then
            height = h
            exit
         end if
      end do
    end associate

  end function dm_point_stratum_height

!------------------------------------------------------------------------
! DM utilities:
!------------------------------------------------------------------------

  subroutine set_dm_data_layout(dm, num_components, field_dim, &
       field_name)
    !! Sets data layout on default section of the given DM.

    DM, intent(in out) :: dm !! DM object
    PetscInt, target, intent(in) :: num_components(:) !! Number of components in each field
    PetscInt, intent(in) :: field_dim(:)  !! Dimension each field is defined on (0 = nodes, etc.)
    character(*), intent(in), optional :: field_name(:) !! Name of each field
    ! Locals:
    PetscInt :: dim
    PetscSection :: section
    PetscInt :: num_fields, i, num_bc
    PetscInt, allocatable, target :: num_dof(:)
    PetscInt, target :: bc_field(1)
    IS, target :: bc_comps(1), bc_points(1)
    PetscInt, pointer :: pnum_components(:), pnum_dof(:), pbc_field(:)
    IS, pointer :: pbc_comps(:), pbc_points(:)
    PetscErrorCode :: ierr

    call DMGetDimension(dm, dim, ierr); CHKERRQ(ierr)
    num_fields = size(num_components)
    allocate(num_dof(num_fields*(dim+1)))
    num_dof = 0
    do i = 1, num_fields
       num_dof((i-1) * (dim+1) + field_dim(i) + 1) = num_components(i)
    end do

    ! Boundary conditions (none):
    num_bc = 0
    bc_field(1) = 0

    pnum_components => num_components
    pnum_dof => num_dof
    pbc_field => bc_field
    pbc_comps => bc_comps
    pbc_points => bc_points

    call DMPlexCreateSection(dm, dim, num_fields, pnum_components, &
         pnum_dof, num_bc, pbc_field, pbc_comps, pbc_points, &
         PETSC_NULL_IS, section, ierr); CHKERRQ(ierr)

    if (present(field_name)) then
       do i = 1, num_fields
          call PetscSectionSetFieldName(section, i-1, field_name(i), ierr)
          CHKERRQ(ierr)
       end do
    end if

    call DMSetSection(dm, section, ierr); CHKERRQ(ierr)
    ! Create the global section:
    call DMGetGlobalSection(dm, section, ierr); CHKERRQ(ierr)
    deallocate(num_dof)

  end subroutine set_dm_data_layout

!------------------------------------------------------------------------

  subroutine set_dm_default_data_layout(dm, dof)
    !! Sets default data layout on DM, for primary variable vector
    !! with specified number of degrees of freedom.

    DM, intent(in out) :: dm
    PetscInt, intent(in) :: dof
    ! Locals:
    PetscInt :: num_components(1), dim, field_dim(1)
    character(7) :: field_names(1)
    PetscErrorCode :: ierr

    call DMGetDimension(dm, dim, ierr); CHKERRQ(ierr)
    num_components = dof
    field_dim = dim
    field_names(1) = "Primary"

    call set_dm_data_layout(dm, num_components, field_dim, &
         field_names)

  end subroutine set_dm_default_data_layout

!------------------------------------------------------------------------

  subroutine dm_setup_global_section(dm)
    !! Sets up global section on DM, based on local section and point
    !! SF.

    DM, intent(in out) :: dm
    ! Locals:
    PetscSF :: sf
    PetscSection :: local_section, global_section
    PetscErrorCode :: ierr

    call DMGetPointSF(dm, sf, ierr); CHKERRQ(ierr)
    call DMGetSection(dm, local_section, ierr); CHKERRQ(ierr)
    call PetscSectionCreateGlobalSection(local_section, sf, PETSC_FALSE, &
         PETSC_FALSE, global_section, ierr); CHKERRQ(ierr)
    call DMSetGlobalSection(dm, global_section, ierr)
    CHKERRQ(ierr)

  end subroutine dm_setup_global_section

!------------------------------------------------------------------------

  subroutine global_vec_range_start(v, range_start)
    !! Gets global start of global range from PetscLayout of global section
    !! on DM that vector v is defined on.

    Vec, intent(in) :: v !! Global vector
    PetscInt, intent(out) :: range_start !! Range start for global section
    ! Locals:
    PetscSection :: section
    PetscLayout :: layout
    PetscInt :: range_end
    PetscErrorCode :: ierr

    call global_vec_section(v, section)

    call PetscSectionGetValueLayout(PETSC_COMM_WORLD, section, layout, &
         ierr); CHKERRQ(ierr)
    call PetscLayoutGetRange(layout, range_start, range_end, ierr)
    CHKERRQ(ierr)
    call PetscLayoutDestroy(layout, ierr)
    CHKERRQ(ierr)

  end subroutine global_vec_range_start

!------------------------------------------------------------------------

  subroutine section_offset(section, p, offset, ierr)
    !! Wrapper for PetscSectionGetOffset(), adding one to the result for
    !! Fortran 1-based indexing.

    PetscSection, intent(in) :: section !! Local section
    PetscInt, intent(in) :: p !! Mesh point in DM
    PetscInt, intent(out) :: offset !! Offset value
    PetscErrorCode, intent(out) :: ierr !! Error flag

    call PetscSectionGetOffset(section, p, offset, ierr)
    offset = offset + 1

  end subroutine section_offset

!------------------------------------------------------------------------

  subroutine global_section_offset(section, p, range_start, offset, ierr)
    !! Wrapper for PetscSectionGetOffset(), adding one to the result for
    !! Fortran 1-based indexing. For global sections, we also need to
    !! subtract the layout range start to get indices suitable for
    !! indexing into arrays.

    PetscSection, intent(in) :: section !! Global section
    PetscInt, intent(in) :: p !! Mesh point
    PetscInt, intent(in) :: range_start !! Start of PetscLayout range
    PetscInt, intent(out) :: offset !! Offset value
    PetscErrorCode, intent(out) :: ierr !! Error flag
    ! Locals:

    call PetscSectionGetOffset(section, p, offset, ierr)
    offset = offset + 1 - range_start

  end subroutine global_section_offset

!------------------------------------------------------------------------

  subroutine global_vec_section(v, section)
    !! Gets default global PETSc section from DM of a vector v, and
    !! range_start from the PetscLayout of the section.

    Vec, intent(in) :: v !! Global vector
    PetscSection, intent(out) :: section !! Default global section
    ! Locals:
    DM :: dm
    PetscErrorCode :: ierr

    call VecGetDM(v, dm, ierr); CHKERRQ(ierr)
    call DMGetGlobalSection(dm, section, ierr); CHKERRQ(ierr)

  end subroutine global_vec_section

!------------------------------------------------------------------------

  subroutine local_vec_section(v, section)
    !! Gets default local PETSc section from DM of a vector v.

    Vec, intent(in) :: v !! Local vector
    PetscSection, intent(out) :: section !! Default local section
    ! Locals:
    DM :: dm
    PetscErrorCode :: ierr

    call VecGetDM(v, dm, ierr); CHKERRQ(ierr)
    call DMGetSection(dm, section, ierr); CHKERRQ(ierr)

  end subroutine local_vec_section

!------------------------------------------------------------------------

  subroutine global_to_local_vec_section(v, local_v, section)
    !! Takes a global vector v and returns a local vector, with values
    !! scattered from the global vector, and the default local PETSc
    !! section from the DM of the global vector.

    Vec, intent(in) :: v !! Global vector
    Vec, intent(in out) :: local_v !! Local vector
    PetscSection, intent(out) :: section !! Default local section
    ! Locals:
    DM :: dm
    PetscErrorCode :: ierr

    call VecGetDM(v, dm, ierr); CHKERRQ(ierr)
    call DMGetSection(dm, section, ierr); CHKERRQ(ierr)

    call DMGetLocalVector(dm, local_v, ierr); CHKERRQ(ierr)
    call DMGlobalToLocalBegin(dm, v, INSERT_VALUES, local_v, ierr)
    CHKERRQ(ierr)
    call DMGlobalToLocalEnd(dm, v, INSERT_VALUES, local_v, ierr)
    CHKERRQ(ierr)

  end subroutine global_to_local_vec_section

!------------------------------------------------------------------------

  subroutine restore_dm_local_vec(local_v)
    !! Restores local vector to its DM.

    Vec, intent(in out) :: local_v !! Local vector
    ! Locals:
    DM :: dm
    PetscErrorCode :: ierr

    call VecGetDM(local_v, dm, ierr); CHKERRQ(ierr)
    call DMRestoreLocalVector(dm, local_v, ierr); CHKERRQ(ierr)

  end subroutine restore_dm_local_vec

!------------------------------------------------------------------------

  subroutine write_vec_vtk(v, filename)
    !! Writes vector v to VTK file.

    Vec, intent(in) :: v !! Vector
    character(len = *), intent(in) :: filename !! VTK output filename
    ! Locals:
    PetscViewer :: viewer
    PetscErrorCode :: ierr

    call PetscViewerCreate(PETSC_COMM_WORLD, viewer, ierr); CHKERRQ(ierr)
    call PetscViewerSetType(viewer, PETSCVIEWERVTK, ierr); CHKERRQ(ierr)
    call PetscViewerFileSetName(viewer, filename, ierr); CHKERRQ(ierr)
    call VecView(v, viewer, ierr); CHKERRQ(ierr)
    call PetscViewerDestroy(viewer, ierr); CHKERRQ(ierr)

  end subroutine write_vec_vtk

!------------------------------------------------------------------------

  subroutine vec_reorder(v, old_index, new_index)
    !! Reorders a vector from the specified old ordering to new
    !! ordering.

    Vec, intent(in out) :: v !! Global vector to be reordered
    IS, intent(in) :: old_index !! Index set for old ordering
    IS, intent(in) :: new_index !! Index set for new ordering
    ! Locals:
    Vec :: vinitial
    VecScatter :: scatter
    PetscInt :: blocksize
    IS :: old_index_block, new_index_block
    PetscInt, pointer :: indices(:)
    PetscErrorCode :: ierr

    call VecDuplicate(v, vinitial, ierr); CHKERRQ(ierr)
    call VecCopy(v, vinitial, ierr); CHKERRQ(ierr)
    call VecGetBlockSize(v, blocksize, ierr); CHKERRQ(ierr)

    call ISGetIndicesF90(old_index, indices, ierr); CHKERRQ(ierr)
    call ISCreateBlock(PETSC_COMM_WORLD, blocksize, size(indices), &
         indices, PETSC_COPY_VALUES, old_index_block, ierr); CHKERRQ(ierr)
    call ISRestoreIndicesF90(old_index, indices, ierr); CHKERRQ(ierr)

    call ISGetIndicesF90(new_index, indices, ierr); CHKERRQ(ierr)
    call ISCreateBlock(PETSC_COMM_WORLD, blocksize, size(indices), &
         indices, PETSC_COPY_VALUES, new_index_block, ierr); CHKERRQ(ierr)
    call ISRestoreIndicesF90(new_index, indices, ierr); CHKERRQ(ierr)

    call VecScatterCreateWithData(vinitial, old_index_block, v, &
         new_index_block, scatter, ierr); CHKERRQ(ierr)
    call ISDestroy(old_index_block, ierr); CHKERRQ(ierr)
    call ISDestroy(new_index_block, ierr); CHKERRQ(ierr)

    call VecScatterBegin(scatter, vinitial, v, INSERT_VALUES, &
         SCATTER_FORWARD, ierr); CHKERRQ(ierr)
    call VecScatterEnd(scatter, vinitial, v, INSERT_VALUES, &
         SCATTER_FORWARD, ierr); CHKERRQ(ierr)

    call VecScatterDestroy(scatter, ierr); CHKERRQ(ierr)
    call VecDestroy(vinitial, ierr); CHKERRQ(ierr)

  end subroutine vec_reorder

!------------------------------------------------------------------------

  subroutine dm_cell_normal_face(dm, c, normal, f)
    !! Returns index of DM mesh face on the specified cell c, with
    !! outward normal vector closest to the specified one.

    use kinds_module

    DM, intent(in) :: dm !! DM
    PetscInt, intent(in) :: c !! Cell mesh point in DM
    PetscReal, intent(in) :: normal(:) !! Normal vector
    PetscInt, intent(out) :: f !! Face mesh point in DM
    ! Locals:
    PetscInt :: i, num_faces, imax, dim, num_cells
    PetscInt :: start_cell, end_cell
    PetscErrorCode :: ierr
    PetscInt, pointer :: faces(:)
    PetscReal, allocatable, target :: centroid(:), face_normal(:)
    PetscReal, pointer :: pcentroid(:), pface_normal(:)
    PetscReal, allocatable :: cos_theta(:)
    PetscReal :: area, normal_norm, face_normal_norm

    call DMGetDimension(dm, dim, ierr); CHKERRQ(ierr)
    call DMPlexGetHeightStratum(dm, 0, start_cell, &
         end_cell, ierr); CHKERRQ(ierr)

    if ((start_cell <= c) .and. (c < end_cell)) then

       allocate(centroid(3), face_normal(3))
       pcentroid => centroid
       pface_normal => face_normal
       call DMPlexGetConeSize(dm, c, num_faces, ierr); CHKERRQ(ierr)
       call DMPlexGetCone(dm, c, faces, ierr); CHKERRQ(ierr)
       allocate(cos_theta(num_faces))
       cos_theta = -1._dp
       normal_norm = norm2(normal(1: dim))

       do i = 1, num_faces
          call DMPlexGetSupportSize(dm, faces(i), num_cells, ierr)
          CHKERRQ(ierr)
          if (num_cells < 2) then
             call DMPlexComputeCellGeometryFVM(dm, faces(i), area, &
                  pcentroid, pface_normal, ierr); CHKERRQ(ierr)
             face_normal_norm = norm2(face_normal(1: dim))
             cos_theta(i) = dot_product(normal(1: dim), &
                  face_normal(1: dim)) / &
                  (normal_norm * face_normal_norm)
          end if
       end do

       imax = maxloc(cos_theta, 1)
       f = faces(imax)

       nullify(pcentroid, pface_normal)
       deallocate(cos_theta, centroid, face_normal)

    else
       f = -1
    end if

  end subroutine dm_cell_normal_face

!------------------------------------------------------------------------

  subroutine vec_max_pointwise_abs_scale(v, scale, tol, maxval, maxloc)
    !! Returns pointwise max absolute value of v / abs(scale), and the
    !! index at which the maximum occurs. Where the value of scale is
    !! less than tol, the value of tol is used instead.

    Vec, intent(in) :: v !! Value vector
    Vec, intent(in) :: scale !! Scale vector
    PetscReal, intent(in) :: tol !! Tolerance for small values
    PetscReal, intent(out) :: maxval !! Maximum value
    PetscInt, intent(out) :: maxloc !! Index of maximum value
    ! Locals:
    DM :: dm
    Vec :: scaled_v
    PetscReal, pointer, contiguous :: v_array(:), scale_array(:)
    PetscReal, pointer, contiguous :: scaled_v_array(:)
    PetscInt :: i, low, hi
    PetscReal :: scale_i
    PetscErrorCode :: ierr

    call VecGetDM(v, dm, ierr); CHKERRQ(ierr)
    call DMGetGlobalVector(dm, scaled_v, ierr); CHKERRQ(ierr)

    call VecGetArrayReadF90(v, v_array, ierr); CHKERRQ(ierr)
    call VecGetArrayReadF90(scale, scale_array, ierr); CHKERRQ(ierr)
    call VecGetArrayF90(scaled_v, scaled_v_array, ierr); CHKERRQ(ierr)

    call VecGetOwnershipRange(v, low, hi, ierr); CHKERRQ(ierr)
    do i = 1, hi - low
       scale_i = max(abs(scale_array(i)), tol)
       scaled_v_array(i) = abs(v_array(i)) / scale_i
    end do

    call VecRestoreArrayReadF90(v, v_array, ierr); CHKERRQ(ierr)
    call VecRestoreArrayReadF90(scale, scale_array, ierr); CHKERRQ(ierr)
    call VecRestoreArrayF90(scaled_v, scaled_v_array, ierr)
    CHKERRQ(ierr)

    call VecMax(scaled_v, maxloc, maxval, ierr); CHKERRQ(ierr)

    call DMRestoreGlobalVector(dm, scaled_v, ierr); CHKERRQ(ierr)

  end subroutine vec_max_pointwise_abs_scale

!------------------------------------------------------------------------

  PetscInt function dm_get_num_partition_ghost_cells(dm) result(n)
    !! Returns number of DM partition ghost cells on current process.

    DM, intent(in) :: dm
    ! Locals:
    PetscMPIInt :: np
    PetscSF :: point_sf
    PetscInt :: start_cell, end_cell
    PetscInt :: num_roots, num_leaves
    PetscInt, pointer :: local(:)
    type(PetscSFNode), pointer :: remote(:)
    PetscErrorCode :: ierr

    call MPI_comm_size(PETSC_COMM_WORLD, np, ierr)
    if (np > 1) then
       call DMPlexGetHeightStratum(dm, 0, start_cell, end_cell, ierr)
       call DMGetPointSF(dm, point_sf, ierr); CHKERRQ(ierr)
       call PetscSFGetGraph(point_sf, num_roots, num_leaves, &
            local, remote, ierr); CHKERRQ(ierr)
       n = count(local < end_cell)
    else
       n = 0
    end if

  end function dm_get_num_partition_ghost_cells

!------------------------------------------------------------------------

  PetscInt function dm_get_bdy_cell_shift(dm) result(shift)
    !! Returns number of boundary cells on processes of rank lower
    !! than the current process.

    use utils_module, only: get_mpi_int_gather_array, array_cumulative_sum

    DM, intent(in) :: dm
    ! Locals:
    PetscInt :: start_cell, end_cell, end_interior_cell
    PetscInt :: num_bdy_cells
    PetscMPIInt :: rank, np
    PetscInt, allocatable :: proc_num_bdy_cells(:), &
         proc_sum_bdy_cells(:)
    PetscErrorCode :: ierr

    call MPI_comm_rank(PETSC_COMM_WORLD, rank, ierr)
    call MPI_comm_size(PETSC_COMM_WORLD, np, ierr)

    call DMPlexGetHeightStratum(dm, 0, start_cell, end_cell, ierr)
    CHKERRQ(ierr)
    end_interior_cell = dm_get_end_interior_cell(dm, end_cell)
    num_bdy_cells = end_cell - end_interior_cell

    proc_num_bdy_cells = get_mpi_int_gather_array()
    proc_sum_bdy_cells = get_mpi_int_gather_array()

    call MPI_gather(num_bdy_cells, 1, MPI_INTEGER, proc_num_bdy_cells, &
         1, MPI_INTEGER, 0, PETSC_COMM_WORLD, ierr)
    if (rank == 0) then
       proc_sum_bdy_cells = [[0], &
            array_cumulative_sum(proc_num_bdy_cells(1: np - 1))]
    end if
    call MPI_scatter(proc_sum_bdy_cells, 1, MPI_INTEGER, shift, &
         1, MPI_INTEGER, 0, PETSC_COMM_WORLD, ierr)

    deallocate(proc_num_bdy_cells, proc_sum_bdy_cells)

  end function dm_get_bdy_cell_shift

!------------------------------------------------------------------------

  PetscInt function dm_get_end_interior_cell(dm, end_cell) &
       result(end_interior_cell)
    !! Returns index (+1) of last interior (i.e. non-boundary) cell of
    !! the DM. In the serial case the result from
    !! DMPlexGetHybridBounds() is -1, so here this is corrected to
    !! end_cell.

    DM, intent(in) :: dm
    PetscInt, intent(in) :: end_cell
    ! Locals:
    PetscInt :: dummy
    PetscErrorCode :: ierr

    call DMPlexGetHybridBounds(dm, end_interior_cell, dummy, &
         dummy, dummy, ierr); CHKERRQ(ierr)
    if (end_interior_cell < 0) end_interior_cell = end_cell

  end function dm_get_end_interior_cell

!------------------------------------------------------------------------

  AO function dm_get_natural_to_global_ao(dm, dist_sf) result(ao)
    !! Returns application ordering for natural to global mapping on
    !! the DM, given the mesh distribution SF.

    DM, intent(in) :: dm
    PetscSF, intent(in) :: dist_sf

    PetscMPIInt :: np
    PetscInt :: num_roots, num_leaves, c
    PetscInt :: start_cell, end_cell, end_interior_cell, end_non_ghost_cell
    PetscInt :: num_ghost_cells, num_non_ghost_cells
    PetscInt, pointer :: local(:)
    type(PetscSFNode), pointer :: remote(:)
    PetscInt, allocatable :: natural(:), global(:)
    ISLocalToGlobalMapping :: l2g
    PetscErrorCode :: ierr

    call DMPlexGetHeightStratum(dm, 0, start_cell, end_cell, &
         ierr); CHKERRQ(ierr)
    end_interior_cell = dm_get_end_interior_cell(dm, end_cell)
    call MPI_comm_size(PETSC_COMM_WORLD, np, ierr)
    if (np > 1) then
       num_ghost_cells = dm_get_num_partition_ghost_cells(dm)
       num_non_ghost_cells = end_interior_cell - start_cell - num_ghost_cells
       end_non_ghost_cell = start_cell + num_non_ghost_cells
       call DMGetLocalToGlobalMapping(dm, l2g, ierr); CHKERRQ(ierr)
       call PetscSFGetGraph(dist_sf, num_roots, num_leaves, &
            local, remote, ierr); CHKERRQ(ierr)
       allocate(natural(start_cell: end_non_ghost_cell - 1), &
            global(start_cell: end_non_ghost_cell - 1))
       natural = remote(1: num_non_ghost_cells)%index
       call ISLocalToGlobalMappingApplyBlock(l2g, num_non_ghost_cells, &
            [(c, c = start_cell, end_non_ghost_cell - 1)], global, ierr)
       CHKERRQ(ierr)
    else ! serial:
       num_non_ghost_cells = end_interior_cell - start_cell
       end_non_ghost_cell = start_cell + num_non_ghost_cells
       natural = [(c, c = start_cell, end_non_ghost_cell - 1)]
       global = natural
    end if
    call AOCreateMapping(PETSC_COMM_WORLD, num_non_ghost_cells, natural, &
         global, ao, ierr); CHKERRQ(ierr)
    deallocate(natural, global)

  end function dm_get_natural_to_global_ao

!------------------------------------------------------------------------

  function natural_to_local_cell_index_array(ao, l2g, natural) &
       result(local)
    !! Returns array of local cell indices corresponding to array
    !! of natural cell indices. Any off-process cells are given index
    !! values of -1.

    AO, intent(in) :: ao !! Application ordering mapping natural to global cell indices
    ISLocalToGlobalMapping, intent(in) :: l2g !! DM local to global mapping
    PetscInt, intent(in) :: natural(:) !! Natural cell indices
    PetscInt :: local(size(natural))
    ! Locals:
    PetscInt :: n, idx(size(natural))
    PetscErrorCode :: ierr

    associate(num_cells => size(natural))
      idx = natural
      call AOApplicationToPetsc(ao, num_cells, idx, ierr); CHKERRQ(ierr)
      call ISGlobalToLocalMappingApplyBlock(l2g, IS_GTOLM_MASK, num_cells, &
           idx, n, local, ierr); CHKERRQ(ierr)
    end associate

  end function natural_to_local_cell_index_array

!------------------------------------------------------------------------

  PetscInt function natural_to_local_cell_index_single(ao, l2g, &
       natural) result(local)
    !! Returns local cell index corresponding to natural cell
    !! index. Off-process cells are given index values of -1.

    AO, intent(in) :: ao !! Application ordering mapping natural to global cell indices
    ISLocalToGlobalMapping, intent(in) :: l2g !! DM local to global mapping
    PetscInt, intent(in) :: natural !! Natural cell index
    ! Locals:
    PetscInt :: n, idx(1), local_array(1)
    PetscErrorCode :: ierr

    idx(1) = natural
    call AOApplicationToPetsc(ao, 1, idx, ierr); CHKERRQ(ierr)
    call ISGlobalToLocalMappingApplyBlock(l2g, IS_GTOLM_MASK, 1, &
         idx, n, local_array, ierr); CHKERRQ(ierr)
    local = local_array(1)

  end function natural_to_local_cell_index_single

!------------------------------------------------------------------------

  function local_to_natural_cell_index_array(ao, l2g, local) &
       result(natural)
    !! Returns array of natural cell indices corresponding to array of
    !! local cell indices.

    AO, intent(in) :: ao !! Application ordering mapping natural to global cell indices
    ISLocalToGlobalMapping, intent(in) :: l2g !! DM local to global mapping
    PetscInt, intent(in) :: local(:) !! Local cell indices
    PetscInt :: natural(size(local))
    ! Locals:
    PetscInt :: idx(size(local))
    PetscErrorCode :: ierr

    associate(num_cells => size(local))
      call ISLocalToGlobalMappingApplyBlock(l2g, num_cells, local, idx, &
           ierr); CHKERRQ(ierr)
      call AOPetscToApplication(ao, num_cells, idx, ierr); CHKERRQ(ierr)
      natural = idx
    end associate

  end function local_to_natural_cell_index_array

!------------------------------------------------------------------------

  PetscInt function local_to_natural_cell_index_single(ao, &
       l2g, local) result(natural)
    !! Returns natural cell index corresponding to local cell index.

    AO, intent(in) :: ao !! Application ordering mapping natural to global cell indices
    ISLocalToGlobalMapping, intent(in) :: l2g !! DM local to global mapping
    PetscInt, intent(in) :: local !! Local cell index
    ! Locals:
    PetscInt :: idx(1), local_array(1)
    PetscErrorCode :: ierr

    local_array(1) = local
    call ISLocalToGlobalMappingApplyBlock(l2g, 1, local_array, idx, ierr)
    CHKERRQ(ierr)
    call AOPetscToApplication(ao, 1, idx, ierr); CHKERRQ(ierr)
    natural = idx(1)

  end function local_to_natural_cell_index_single

!------------------------------------------------------------------------

  subroutine dm_get_cell_index(dm, ao, cell_index)
    !! Returns cell_index IS mapping natural cell indices to global
    !! indices of a global vector (without boundary data included).

    DM, intent(in) :: dm !! Input DM
    AO, intent(in) :: ao !! Natural-to-global AO for the DM
    IS, intent(out) :: cell_index !! Natural-to-global IS (without boundary data)
    ! Locals:
    ISLocalToGlobalMapping :: l2g
    DMLabel :: ghost_label
    PetscInt :: start_cell, end_cell, end_interior_cell
    PetscInt :: c, ic, ghost, carray(1)
    PetscInt :: num_ghost_cells, num_non_ghost_cells, bdy_cell_shift
    PetscInt, allocatable :: global(:), natural(:), global_interior(:)
    PetscInt, allocatable :: index_natural(:), index_global(:)
    AO :: ao_interior
    PetscErrorCode :: ierr

    call DMGetLocalToGlobalMapping(dm, l2g, ierr); CHKERRQ(ierr)
    call DMPlexGetHeightStratum(dm, 0, start_cell, end_cell, &
         ierr); CHKERRQ(ierr)
    end_interior_cell = dm_get_end_interior_cell(dm, end_cell)
    num_ghost_cells = dm_get_num_partition_ghost_cells(dm)
    num_non_ghost_cells = end_interior_cell - start_cell - num_ghost_cells
    bdy_cell_shift = dm_get_bdy_cell_shift(dm)

    allocate(global(0: num_non_ghost_cells - 1))
    ic = 0
    call DMGetLabel(dm, "ghost", ghost_label, ierr); CHKERRQ(ierr)
    do c = start_cell, end_interior_cell - 1
       call DMLabelGetValue(ghost_label, c, ghost, ierr)
       if (ghost < 0) then
          carray = c
          call ISLocalToGlobalMappingApplyBlock(l2g, 1, carray, &
               global(ic:ic), ierr); CHKERRQ(ierr)
          ic = ic + 1
       end if
    end do

    ! The index_natural array does not represent natural indices
    ! corresponding to local indices- it is just a set of natural
    ! indices owned by the current process, for the purpose of writing
    ! out the corresponding global indices:
    index_natural = global - bdy_cell_shift
    natural = global
    call AOPetscToApplication(ao, num_non_ghost_cells, natural, &
         ierr); CHKERRQ(ierr)
    global_interior = global - bdy_cell_shift
    call AOCreateMapping(PETSC_COMM_WORLD, num_non_ghost_cells, &
         natural, global_interior, ao_interior, ierr); CHKERRQ(ierr)
    deallocate(natural, global_interior)
    index_global = index_natural
    call AOApplicationToPetsc(ao_interior, num_non_ghost_cells, &
         index_global, ierr); CHKERRQ(ierr)
    call AODestroy(ao_interior, ierr); CHKERRQ(ierr)
    call ISCreateGeneral(PETSC_COMM_WORLD, num_non_ghost_cells, &
         index_global, PETSC_COPY_VALUES, cell_index, ierr)
    CHKERRQ(ierr)
    call PetscObjectSetName(cell_index, "cell_index", &
         ierr); CHKERRQ(ierr)

    deallocate(global, index_natural, index_global)

  end subroutine dm_get_cell_index

!------------------------------------------------------------------------

  subroutine dm_set_fv_adjacency(dm)
    !! Sets finite-volume adjacency for DM.

    DM, intent(in out) :: dm
    ! Locals:
    PetscErrorCode :: ierr

    call DMPlexSetAdjacencyUseCone(dm, PETSC_TRUE, ierr); CHKERRQ(ierr)
    call DMPlexSetAdjacencyUseClosure(dm, PETSC_FALSE, ierr); CHKERRQ(ierr)

  end subroutine dm_set_fv_adjacency

!------------------------------------------------------------------------

  subroutine dm_setup_fv_discretization(dm, dof)
    !! Sets up finite-volume discretization on DM.

    DM, intent(in out) :: dm
    PetscInt, intent(in) :: dof
    ! Locals:
    PetscFV :: fvm
    PetscDS :: ds
    PetscInt :: dim
    PetscErrorCode :: ierr

    call PetscFVCreate(PETSC_COMM_WORLD, fvm, ierr); CHKERRQ(ierr)
    call PetscFVSetFromOptions(fvm, ierr); CHKERRQ(ierr)
    call PetscFVSetNumComponents(fvm, dof, ierr); CHKERRQ(ierr)
    call DMGetDimension(dm, dim, ierr); CHKERRQ(ierr)
    call PetscFVSetSpatialDimension(fvm, dim, ierr); CHKERRQ(ierr)
    call DMGetDS(dm, ds, ierr); CHKERRQ(ierr)
    call PetscDSAddDiscretization(ds, fvm, ierr); CHKERRQ(ierr)
    call PetscFVDestroy(fvm, ierr); CHKERRQ(ierr)

  end subroutine dm_setup_fv_discretization

!------------------------------------------------------------------------

  subroutine create_path_dm(num_nodes, dm)
    !! Creates a 1-D DMPlex representing a path. Points 0 .. num_nodes
    !! -1 are the nodes; points num_nodes ... 2 * num_nodes - 2 are
    !! the edges.

    PetscInt, intent(in) :: num_nodes !! Number of nodes on the path
    DM, intent(out) :: dm !! Output DMPlex
    ! Locals:
    PetscInt :: num_edges, num_points, p
    PetscErrorCode :: ierr

    call DMPlexCreate(PETSC_COMM_WORLD, dm, ierr); CHKERRQ(ierr)
    call DMSetDimension(dm, 1, ierr); CHKERRQ(ierr)

    if (num_nodes > 0) then
       num_edges = num_nodes - 1
    else
       num_edges = 0
    end if
    num_points = num_nodes + num_edges

    call DMPlexSetChart(dm, 0, num_points, ierr)
    CHKERRQ(ierr)

    do p = num_nodes, num_points - 1
       call DMPlexSetConeSize(dm, p, 2, ierr); CHKERRQ(ierr)
    end do

    call DMSetUp(dm, ierr); CHKERRQ(ierr)

    do p = num_nodes, num_points - 1
       associate (p_node => p - num_nodes)
         call DMPlexSetCone(dm, p, [p_node, p_node + 1], ierr)
         CHKERRQ(ierr)
       end associate
    end do

    call DMPlexSymmetrize(dm, ierr); CHKERRQ(ierr)
    call DMPlexStratify(dm, ierr); CHKERRQ(ierr)

  end subroutine create_path_dm

!------------------------------------------------------------------------

  subroutine get_field_subvector(v, field, index_set, subv)
    !! Gets subvector of v for the specified field. Based on
    !! PetscSectionGetField_Internal().

    Vec, intent(in) :: v
    PetscInt, intent(in) :: field
    IS, intent(in out) :: index_set
    Vec, intent(in out) :: subv
    ! Locals:
    DM :: dm
    PetscSection :: section, global_section
    PetscInt :: pstart, pend, p, f, fc, fdof
    PetscInt :: start_cell, end_cell, end_interior_cell
    PetscInt :: num_components, gdof, poff, goff, suboff
    PetscInt :: subsize
    PetscInt, allocatable :: subindices(:)
    PetscErrorCode :: ierr

    call VecGetDM(v, dm, ierr); CHKERRQ(ierr)
    call DMGetSection(dm, section, ierr); CHKERRQ(ierr)
    call DMGetGlobalSection(dm, global_section, ierr); CHKERRQ(ierr)
    call DMPlexGetHeightStratum(dm, 0, start_cell, end_cell, ierr); CHKERRQ(ierr)
    end_interior_cell = dm_get_end_interior_cell(dm, end_cell)

    call PetscSectionGetChart(section, pstart, pend, ierr); CHKERRQ(ierr)
    ! Modify point range for cells- to exclude boundary ghosts:
    if ((end_cell > start_cell) .and. &
         (start_cell >= pstart) .and. (start_cell < pend)) then
       pend = end_interior_cell
    end if

    call PetscSectionGetFieldComponents(section, field, &
         num_components, ierr); CHKERRQ(ierr)
    subsize = 0
    do p = pstart, pend - 1
       call PetscSectionGetDof(global_section, p, gdof, ierr); CHKERRQ(ierr)
       if (gdof > 0) then
          call PetscSectionGetFieldDof(section, p, field, fdof, ierr)
          CHKERRQ(ierr)
          subsize = subsize + fdof
       end if
    end do
    allocate(subindices(0: subsize - 1))

    suboff = 0
    do p = pstart, pend - 1
       call PetscSectionGetDof(global_section, p, gdof, ierr); CHKERRQ(ierr)
       if (gdof > 0) then
          call PetscSectionGetOffset(global_section, p, goff, ierr)
          CHKERRQ(ierr)
          poff = 0
          do f = 0, field - 1
             call PetscSectionGetFieldDof(section, p, f, fdof, ierr)
             CHKERRQ(ierr)
             poff = poff + fdof
          end do
          call PetscSectionGetFieldDof(section, p, field, fdof, ierr)
          CHKERRQ(ierr)
          subindices(suboff: suboff + fdof - 1) = &
               goff + poff + [(fc, fc = 0, fdof - 1)]
          suboff = suboff + fdof
       end if
    end do

    call ISCreateGeneral(PETSC_COMM_WORLD, subsize, subindices, &
         PETSC_COPY_VALUES, index_set, ierr); CHKERRQ(ierr)
    deallocate(subindices)
    call VecGetSubVector(v, index_set, subv, ierr); CHKERRQ(ierr)
    call VecSetBlockSize(subv, num_components, ierr); CHKERRQ(ierr)

  end subroutine get_field_subvector

!------------------------------------------------------------------------

  subroutine section_get_field_names(section, lowercase, field_names)
    !! Returns array of section field names.

    use utils_module, only: str_to_lower

    PetscSection, intent(in) :: section
    PetscBool, intent(in) :: lowercase
    character(*), allocatable :: field_names(:)
    ! Locals:
    PetscInt :: num_fields, f
    PetscErrorCode :: ierr

    call PetscSectionGetNumFields(section, num_fields, ierr); CHKERRQ(ierr)
    allocate(field_names(num_fields))
    do f = 1, num_fields
       call PetscSectionGetFieldName(section, f - 1, field_names(f), ierr)
       if (lowercase) then
          field_names(f) = str_to_lower(field_names(f))
       end if
    end do

  end subroutine section_get_field_names

!------------------------------------------------------------------------

  PetscInt function dm_global_cell_field_dof(dm, field) result(dof)
    !! Returns degrees of freedom associated with specified DM section
    !! cell field. Checks dof on all processes in case current process
    !! has no cells.

    DM, intent(in) :: dm
    PetscInt, intent(in) :: field
    ! Locals:
    PetscSection :: section
    PetscInt :: start_cell, end_cell, field_dof
    PetscErrorCode :: ierr

    call DMGetSection(dm, section, ierr); CHKERRQ(ierr)
    call DMPlexGetHeightStratum(dm, 0, start_cell, end_cell, ierr)
    if (end_cell > start_cell) then
       call PetscSectionGetFieldDof(section, start_cell, field, &
            field_dof, ierr); CHKERRQ(ierr)
    else
       field_dof = -1
    end if
    call MPI_Allreduce(field_dof, dof, 1, MPI_INT, MPI_MAX, &
         PETSC_COMM_WORLD, ierr); CHKERRQ(ierr)

  end function dm_global_cell_field_dof

!------------------------------------------------------------------------

end module dm_utils_module
